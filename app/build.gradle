plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
}


android {
    namespace 'se.cpacsystems.carevscamera'
    compileSdk 32

    defaultConfig {
        applicationId "se.cpacsystems.carevscamera"
        minSdk 31
        //noinspection OldTargetApi
        targetSdk 32
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        externalNativeBuild {
            cmake {
                cppFlags '-std=c++11'
            }
        }
    }
    signingConfigs {
        release {
            storeFile file("${project.rootDir}/release.keystore")
            storePassword "android"
            keyAlias "android"
            keyPassword "android"
        }
    }

    buildTypes {
        debug {
            signingConfig signingConfigs.release
            jniDebuggable true
            renderscriptDebuggable true
        }
        release {
            signingConfig signingConfigs.release
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    buildFeatures {
        viewBinding true
    }
    externalNativeBuild {
        cmake {
            path file('src/main/cpp/CMakeLists.txt')
            version '3.22.1'

        }
    }
    testOptions {
        unitTests.returnDefaultValues = true
    }
}

dependencies {

    compileOnly fileTree(include: ['*.jar'], dir: "libs")

    debugImplementation 'com.squareup.leakcanary:leakcanary-android:2.9.1'

    //noinspection GradleDependency 1.6.0 requires SDK 33
    implementation 'androidx.appcompat:appcompat:1.5.1'
    implementation 'com.google.android.material:material:1.8.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'androidx.navigation:navigation-fragment:2.5.3'
    implementation 'androidx.navigation:navigation-ui:2.5.3'

    testImplementation fileTree(include: ['*.jar'], dir: "libs")
    testImplementation 'junit:junit:4.13.2'
    testImplementation "org.mockito:mockito-core:4.6.0"
    testImplementation "org.mockito:mockito-inline:4.6.0"
    testImplementation "org.mockito.kotlin:mockito-kotlin:4.0.0"

    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
}

task makeModule(type: Copy) {
    // Copy build artifacts to module
    dependsOn assemble
    from "resources/Android.mk", "resources/privapp_permissions_se.cpacsystems.carevscamera.xml", "build/outputs/apk/release/app-release.apk"
    into "${project.property('aosp.home')}${project.property('vendor.prebuilt')}/evscamera"
    rename ('app-release.apk', 'evscamera.apk')
}
